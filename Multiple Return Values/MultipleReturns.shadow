import shadow:io@Console;

class MultipleReturns
{
	public main( String[] args ) => ()
	{
	 	Console io = Console:instance;
	 	
	 	String temp, evenOdd;
	 	int num, squared, half, doubled, factorial;
	 	
	 	//Prompt the user for an int to inspect	
	 	io.print("\nEnter an Integer: ");
	 	(temp, ) = io.readLine();
	 	num = temp.toInt();
	 	io.printLine();

	 	//This is how you structure multiple returns.
	 	//Place them in the order they get returned from the function	
	 	(evenOdd, half, doubled, squared, factorial) = numberInfo(num);
	 	
	 	//Print out the info	
	 	io.printLine(num # " is an " # evenOdd # " number.");
	 	io.printLine("Half of " # num # " is: " # half);
	 	io.printLine(num # " doubled is: " # doubled);
	 	io.printLine(num # " squared is: " # squared);
	 	io.printLine(num # "! is: " # factorial # "\n");
	 	
	}

	//This is how you declare a function with multiple return types and values 
	 public numberInfo(int num) => (String, int, int, int, int)
	 {
	 	String parity;
	 	int half, doubled, squared, factorial;
	 
	 	//Get all the info on the number	
	 	if(num % 2 == 0)
	 		parity = "even";
	 	else
	 		parity = "odd";
	 		
	 	half = num / 2;
	 	doubled = num * 2;
	 	squared = num * num;
	 	
	 	factorial = 1;
	 	for(int i = 2 ; i <= num; i += 1)
	 		factorial *= i;	
	 	
	 	//Return each value in the order required.
	 	//It must match the declaration of the function.	
	 	return(parity, half, doubled, squared, factorial);
	 }
}